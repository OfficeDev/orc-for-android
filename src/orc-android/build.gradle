apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory-upload'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven'

group = project.mavenGroupId
version = project.mavenVersion

def targetRepo = project.version.endsWith('-SNAPSHOT')?'oss-snapshot-local':'oss-release-local'

android {
    compileSdkVersion 15
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 15
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        release {
            minifyEnabled
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    packagingOptions{
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.google.guava:guava:18.0'
    compile 'com.microsoft.aad:adal:1.1.7'
    compile 'com.microsoft.services.msa:msa-auth:0.8.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath())
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

artifactoryPublish {
    dependsOn sourcesJar
}
/*
configure(install.repositories.mavenInstaller) {
    pom.project {
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}
*/

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = targetRepo
            username = project.hasProperty("bintrayUsername") ? project.bintrayUsername : ""
            password = project.hasProperty("bintrayApikey") ? project.bintrayApikey : ""
        }
        defaults {
            publishConfigs('archives')
            publishPom = true //Publish generated POM files to Artifactory (true by default)
            publishIvy = false //Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = 'oss-release-local'
        }
    }
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        pom {
            setGroupId project.mavenGroupId
            setArtifactId project.mavenArtifactId
            setVersion project.mavenVersion
        }
        repository (url: project.mavenRepoUrl) {
            authentication(
                    // put these values in local file ~/.gradle/gradle.properties
                    userName: project.hasProperty("bintrayUsername") ? project.bintrayUsername : "",
                    password: project.hasProperty("bintrayApikey") ? project.bintrayApikey : ""
            )
        }
    }
}


